plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.5.0'
}

group 'com.github.grishberg'
version ext.productVersion

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url 'https://maven.google.com'
    }
}

if (!hasProperty('studioCompilePath')) {
    throw new GradleException("No StudioCompilePath value was set, please create gradle.properties file")
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    updateSinceUntilBuild false
    plugins 'android'
    intellij.localPath = project.hasProperty("studioCompilePath") ? studioCompilePath : studioRunPath
}

runIde {
    ideDirectory studioCompilePath
}

dependencies {
    implementation project(':core')

    implementation 'com.github.Grigory-Rylov:adb-facade-core:0.1.8'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'com.github.Grigory-Rylov:andoid_method_trace_recorder:2.1.0'

    compileOnly fileTree(dir: "$studioCompilePath/plugins/android/lib", include: ['*.jar'])
    compileOnly fileTree(dir: "$studioCompilePath/lib", include: ['*.jar'])

    testCompile 'junit:junit:4.12'
    testCompile fileTree(dir: "$studioCompilePath/plugins/android/lib", include: ['*.jar'])
    testCompile fileTree(dir: "$studioCompilePath/lib", include: ['*.jar'])

}

task(verifySetup) {
    doLast {
        def ideaJar = "$StudioCompilePath/lib/idea.jar"
        if (!file(ideaJar).exists()) {
            throw new GradleException("$ideaJar not found, set StudioCompilePath in gradle.properties")
        }
    }
}

patchPluginXml {
    changeNotes """
      Get package name from current project if there is no stored one.<br>
      """
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
