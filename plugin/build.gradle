plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.5.0'
}

group 'com.github.grishberg'
version ext.productVersion

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    maven {
        url = uri("https://www.jetbrains.com/intellij-repository/snapshots/")
    }
    maven {
        url = uri("https://oss.sonatype.org/content/repositories/snapshots/")
    }
    maven {
        url = uri("https://www.jetbrains.com/intellij-repository/releases")
    }
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url 'https://maven.google.com'
    }
}

if (!hasProperty('studioCompilePath')) {
    throw new GradleException("No StudioCompilePath value was set, please create gradle.properties file")
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    updateSinceUntilBuild false
    plugins ['android']
    version "212-EAP-SNAPSHOT"
}

runIde {
    ideDirectory studioCompilePath
}

dependencies {
    implementation project(':core')

    implementation 'com.github.Grigory-Rylov:adb-facade-core:0.1.8'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'com.github.Grigory-Rylov:andoid_method_trace_recorder:2.1.0'

    compileOnly fileTree(dir: "$studioCompilePath/plugins/android/lib", include: ['*.jar'])
    compileOnly fileTree(dir: "$studioCompilePath/lib", include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    testImplementation fileTree(dir: "$studioCompilePath/plugins/android/lib", include: ['*.jar'])
    testImplementation fileTree(dir: "$studioCompilePath/lib", include: ['*.jar'])

}

task(verifySetup) {
    doLast {
        def ideaJar = "$StudioCompilePath/lib/idea.jar"
        if (!file(ideaJar).exists()) {
            throw new GradleException("$ideaJar not found, set StudioCompilePath in gradle.properties")
        }
    }
}

patchPluginXml {
    sinceBuild("211")
    changeNotes """
      Fixed swithing threads and other dialogs result.<br>
      """
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}
